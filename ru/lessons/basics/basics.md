---
layout: page
title: Основы
category: basics
order: 1
lang: ru
---

Базовая настройка, типы и операторы.

## Содержание

- [Настройка](#section-1)
  - [Установка Elixir](#elixir)
  - [Интерактивный режим](#section-2)
- [Базовые типы](#section-3)
  - [Целые числа](#section-4)
  - [Числа с плавающей запятой](#section-5)
  - [Логический тип](#section-6)
  - [Атомы](#section-7)
  - [Строки](#section-8)
- [Базовые операторы](#section-9)
  - [Арифметические](#section-10)
  - [Логические](#section-11)
  - [Сравнение](#section-12)
  - [Строковая интерполяция](#section-13)
  - [Обьединение строк](#section-14)

## Настройка

### Установка Elixir
Инструкции по установки для всех ОС есть на [официальном сайте](http://elixir-lang.org/install.html).

### Интерактивный режим

Вместе с языком в комплекте идет приложение интерактивной командной строки `iex`, которое позволяет выполнять выражения языка на лету.

Для того чтобы начать, запустите `iex`:

  Erlang/OTP 17 [erts-6.4] [source] [64-bit] [smp:8:8] [async-threads:10] [hipe] [kernel-poll:false] [dtrace]

  Interactive Elixir (1.0.4) - press Ctrl+C to exit (type h() ENTER for help)
  iex>

## Базовые типы

### Целые числа

```elixir
iex> 255
255
iex> 0xFF
255
```

Поддержка бинарных, восьмеричных и шестнадцатеричных чисел уже встроена:

```elixir
iex> 0b0110
6
iex> 0o644
420
iex> 0x1F
31
```

### Числа с плавающей запятой

В Elixir, числа с плавающей запятой требуют наличия хотя бы одной цифры перед точкой. Также они поддерживают `e` для описания экспонентной части:

```elixir
iex> 3.41
3.41
iex> .41
** (SyntaxError) iex:2: syntax error before: '.'
iex> 1.0e-10
1.0e-10
```


### Логический тип

Elixir поддерживает два значения логического типа: `true` и `false`. Абсолютно все значения в языке считаются истинными кроме `false` и `nil`:


```elixir
iex> true
true
iex> false
false
```

### Атомы

Атом - константа, название которой является и значением. В других языках (например, в Ruby) они называются символами:

```elixir
iex> :foo
:foo
iex> :foo == :bar
false
```

Стоит заметить что булевы значения `true`, `false` являются атомами `:true` и `:false`


```elixir
iex> true |> is_atom
true
iex> :true |> is_boolean
true
iex> :true === true
true
```

### Строки

Строки в Elixir всегда в кодировке UTF-8 и находятся в двойных кавычках:

```elixir
iex> "Hello"
"Hello"
iex> "dziękuję"
"dziękuję"
```

Строки могут включать разрывы строки и экранированные последовательности:

```elixir
iex> "foo
...> bar"
"foo\nbar"
iex> "foo\nbar"
"foo\nbar"
```

## Базовые операторы

### Арифметика

В Elixir базовые операторы `+`, `-`, `*`, `/` обладают ожидаемым поведением. Стоит отметить что результатом вызова `/` всегда будет число с плавающей запятой:


```elixir
iex> 2 + 2
4
iex> 2 - 1
1
iex> 2 * 5
10
iex> 10 / 5
2.0
```

Если нужно целочисленное деление или получение остатка - в языке есть две удобные функции специально для этого:

```elixir
iex> div(10, 5)
2
iex> rem(10, 3)
1
```

### Логические операторы

Elixir предоставляет операторы `||`, `&&`, `!`, которые поддерживают работу с любыми типами:

```elixir
iex> -20 || true
-20
iex> false || 42
42

iex> 42 && true
true
iex> 42 && nil
nil

iex> !42
false
iex> !false
true
```

Также есть три дополнительных оператора, у которых первый аргумент обязан быть логического типа (`true` или `false`):


```elixir
iex> true and 42
42
iex> false or true
true
iex> not false
true
iex> 42 and true
** (ArgumentError) argument error: 42
iex> not 42
** (ArgumentError) argument error
```

### Сравнения

В Elixir поддерживаются все стандартные операторы сравнения: `==`, `!=`, `===`, `!==`, `<=`, `>=`, `<`, `>`.

```elixir
iex> 1 > 2
false
iex> 1 != 2
true
iex> 2 == 2
true
iex> 2 <= 3
true
```

Для строгого сравнения целых чисел и чисел с плавающей запятой используется `===`:

```elixir
iex> 2 == 2.0
true
iex> 2 === 2.0
false
```

Удобной возможностью языка является то, что любые типы сравнимы друг с другом. Это удобно при сортировках. Порядок не стоит запоминать, но о его существовании стоит знать:

```elixir
number < atom < reference < functions < port < pid < tuple < maps < list < bitstring
```

Это приводит к некоторым интересным правильным сравнениям, которых обычно нет в других языках программирования:

```elixir
iex> :hello > 999
true
iex> {:hello, :world} > [1, 2, 3]
false
```

### Интерполяция строк

Если вы использовали язык Ruby, то интерполяция в Elixir может показаться знакомой:

```elixir
iex> name = "Sean"
iex> "Hello #{name}"
"Hello Sean"
```

### Объединение строк

Для объединения двух строк используется оператор `<>`:

```elixir
iex> name = "Sean"
iex> "Hello " <> name
"Hello Sean"
```
